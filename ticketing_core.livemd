# Ticketing core

```elixir
# Copy pasta from Ash Livebook Tutorial
Application.put_env(:ash, :validate_api_resource_inclusion?, false)
Application.put_env(:ash, :validate_api_config_inclusion?, false)
Mix.install([:ash], consolidate_protocols: false)
```

## Core definitions in Ash

This is all Copy/paste from the ash tutorial as a starting point. Check the commit history for changes.

1. Copy paste, then rename variables with no functional changes.

```elixir
defmodule BoxOffice.Core.Patron do
  use Ash.Resource,
    data_layer: Ash.DataLayer.Ets

  actions(do: defaults([:create, :read, :update, :destroy]))

  attributes do
    uuid_primary_key(:id)
    attribute(:name, :string)
  end

  # Added the inverse relationship of belongs_to in the ticket.
  # This way we can reference :tickets inside the aggregates.
  relationships do
    has_many(:tickets, BoxOffice.Core.Ticket)
  end

  code_interface do
    define_for(BoxOffice.Core)

    define(:create, args: [:name])
  end

  # <- Add the aggregates here
  aggregates do
    count :count_of_open_tickets, :tickets do
      filter(expr(status == :open))
    end
  end
end

defmodule BoxOffice.Core.Ticket do
  use Ash.Resource,
    data_layer: Ash.DataLayer.Ets

  actions do
    defaults([:read, :update])

    # On creation set the patron by providing the id
    create(:open, do: accept([:subject, :description, :patron_id]))

    update :close do
      accept([])
      change(set_attribute(:status, :closed))
    end

    update :assign do
      accept([])
      argument(:patron_id, :uuid, allow_nil?: false)
      change(manage_relationship(:patron_id, :patron, type: :append_and_remove))
    end
  end

  attributes do
    uuid_primary_key(:id)
    attribute(:subject, :string, allow_nil?: false)
    attribute(:description, :string, allow_nil?: true)

    attribute :status, :atom do
      constraints(one_of: [:open, :closed])
      default(:open)
      allow_nil?(false)
    end

    create_timestamp(:created_at)
    update_timestamp(:updated_at)
  end

  relationships do
    belongs_to(:patron, BoxOffice.Core.Patron) do
      # Set to writable so you can directly set the patron_id inside the :open action
      attribute_writable?(true)
    end
  end

  code_interface do
    define_for(BoxOffice.Core)

    define(:assign, args: [:patron_id])
    # <- added patron_id
    define(:open, args: [:subject, :description, :patron_id])
    define(:close, args: [])
  end
end

defmodule BoxOffice.Core do
  use Ash.Api

  resources do
    resource(BoxOffice.Core.Ticket)
    resource(BoxOffice.Core.Patron)
  end
end
```
