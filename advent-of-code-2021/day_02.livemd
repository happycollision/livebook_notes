<!-- livebook:{"persist_outputs":true} -->

# Advent of Code 2021 - Day 2: Dive!

## Setup

```elixir
course_commands = File.read!("./advent-of-code-2021/day_02_input.txt")

test_course_commands = """
forward 5
down 5
forward 8
up 3
down 8
forward 2
"""
```

```output
"forward 5\ndown 5\nforward 8\nup 3\ndown 8\nforward 2\n"
```

## Part 1

> Now, you need to figure out how to pilot this thing.
> 
> It seems like the submarine can take a series of commands like `forward 1`, `down 2`, or `up 3`:
> 
> * `forward X` increases the horizontal position by X units.
> * `down X` increases the depth by X units.
> * `up X` decreases the depth by X units.
> 
> Note that since you're on a submarine, down and up affect your depth, and so they have the opposite result of what you might expect.
> 
> The submarine seems to already have a planned course (your puzzle input). You should probably figure out where it's going. For example:
> 
> ```
> forward 5
> down 5
> forward 8
> up 3
> down 8
> forward 2
> ```
> 
> Your horizontal position and depth both start at `0`. The steps above would then modify them as follows:
> 
> * `forward 5` adds `5` to your horizontal position, a total of `5`.
> * `down 5` adds `5` to your depth, resulting in a value of `5`.
> * `forward 8` adds `8` to your horizontal position, a total of `13`.
> * `up 3` decreases your depth by `3`, resulting in a value of `2`.
> * `down 8` adds `8` to your depth, resulting in a value of `10`.
> * `forward 2` adds `2` to your horizontal position, a total of `15`.
> 
> After following these instructions, you would have a horizontal position of `15` and a depth of `10`. (Multiplying these together produces `150`.)
> 
> Calculate the horizontal position and depth you would have after following the planned course. What do you get if you multiply your final horizontal position by your final depth?

```elixir
defmodule Command do
  def interpret("forward " <> num_str) do
    %{x: String.to_integer(num_str), y: 0}
  end

  def interpret("down " <> num_str) do
    %{x: 0, y: String.to_integer(num_str)}
  end

  def interpret("up " <> num_str) do
    %{x: 0, y: -String.to_integer(num_str)}
  end
end

course_commands
|> String.split("\n", trim: true)
|> Enum.map(&Command.interpret/1)
|> Enum.reduce(fn term, acc -> %{x: term.x + acc.x, y: term.y + acc.y} end)
|> (&(&1.x * &1.y)).()
```

```output
1893605
```

## Part 2

> Based on your calculations, the planned course doesn't seem to make any sense. You find the submarine manual and discover that the process is actually slightly more complicated.
> 
> In addition to horizontal position and depth, you'll also need to track a third value, aim, which also starts at 0. The commands also mean something entirely different than you first thought:
> 
> * `down X` _increases_ your aim by `X` units.
> * `up X` _decreases_ your aim by `X` units.
> * `forward X` does two things:
>   * It increases your horizontal position by `X` units.
>   * It increases your depth by your aim multiplied by `X`.
> 
> Again note that since you're on a submarine, down and up do the opposite of what you might expect: "down" means aiming in the positive direction.
> 
> Now, the above example does something different:
> 
> * `forward 5` adds `5` to your horizontal position, a total of `5`. Because your aim is `0`, your depth does not change.
> * `down 5` adds `5` to your aim, resulting in a value of `5`.
> * `forward 8` adds `8` to your horizontal position, a total of `13`. Because your aim is `5`, your depth increases by `8*5=40`.
> * `up 3` decreases your aim by `3`, resulting in a value of `2`.
> * `down 8` adds `8` to your aim, resulting in a value of `10`.
> * `forward 2` adds `2` to your horizontal position, a total of `15`.Because your aim is `10`, your depth increases by `2*10=20` to a total of `60`.
> 
> After following these new instructions, you would have a horizontal position of `15` and a depth of `60`. (Multiplying these produces `900`.)
> 
> Using this new interpretation of the commands, calculate the horizontal position and depth you would have after following the planned course. What do you get if you multiply your final horizontal position by your final depth?

```elixir
defmodule Command do
  def gather(list) when is_list(list) do
    Enum.map(list, &interpret/1)
  end

  defp interpret("forward " <> num_str) do
    {:x, String.to_integer(num_str)}
  end

  defp interpret("down " <> num_str) do
    {:a, String.to_integer(num_str)}
  end

  defp interpret("up " <> num_str) do
    {:a, -String.to_integer(num_str)}
  end

  def execute(list) when is_list(list) do
    Enum.reduce(list, %{x: 0, y: 0, a: 0}, &reduce/2)
  end

  defp reduce({:a, num}, acc) do
    Map.update!(acc, :a, fn a -> a + num end)
  end

  defp reduce({:x, num}, %{x: x, y: y, a: a}) do
    %{a: a, x: x + num, y: y + a * num}
  end
end

course_commands
|> String.split("\n", trim: true)
|> Command.gather()
|> Command.execute()
|> (&(&1.x * &1.y)).()
```

```output
2120734350
```
